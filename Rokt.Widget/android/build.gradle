buildscript {
    ext {
        kotlin_version = '1.9.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

def isNewArchitectureEnabled() {
    // The new architecture is not yet public
    // We can only use the new architecture in build time, not runtime
    return project.hasProperty("newArchEnabled") ? project.newArchEnabled.toBoolean() : false
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
if (isNewArchitectureEnabled()) {
    apply plugin: 'com.facebook.react'
}

android {
    compileSdk 35
    buildToolsVersion '35.0.0'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 2
        versionName "2.0"
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    }
    lintOptions {
        abortOnError false
    }
    buildFeatures {
        buildConfig = true
    }
    namespace 'com.rokt.reactnativesdk'
    sourceSets {
        main {
            if (isNewArchitectureEnabled()) {
                java.srcDirs += ['src/newarch']
            } else {
                java.srcDirs += ['src/oldarch']
            }
        }
    }
}

repositories {

        mavenLocal()

        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }

        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }

        maven {
            url  "https://apps.rokt.com/msdk"
        }

        google()
        mavenCentral()
        maven {
            name = "Central Portal Snapshots"
            url = uri("https://central.sonatype.com/repository/maven-snapshots/")
            mavenContent {
                snapshotsOnly()
            }
        }
}

dependencies {
    implementation "com.facebook.react:react-native:+"  // From node_modules
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation ('com.rokt:roktsdk:4.8.1')
}

if (isNewArchitectureEnabled()) {
    react {
        jsRootDir = file("../src/")
        libraryName = "RNRoktWidget"
        codegenJavaPackageName = "com.rokt.reactnativesdk"
    }
}
